# Get zplug if it doesn't exist
ZPLUG_HOME=~/.zplug
ZPLUG_SHALLOW=false
if [ ! -f $ZPLUG_HOME/zplug ]; then
    if hash curl 2>/dev/null; then
        curl -fLo $ZPLUG_HOME/zplug --create-dirs https://git.io/zplug
    elif hash wget 2>/dev/null; then
        if [ ! -d $ZPLUG_HOME ]; then
            mkdir -p $ZPLUG_HOME
        fi
        wget https://git.io/zplug -O $ZPLUG_HOME/zplug
    else
        echo "Couldn't download zplug" 1>&2
        return 1
    fi

    source ~/.zplug/zplug && zplug update --self
fi

source $ZPLUG_HOME/zplug
zplug 'b4b4r07/zplug'

zplug 'NigoroJr/do-after', \
    as:command, \
    use:do-after
# Completion
zplug 'NigoroJr/do-after', \
    as:plugin

zplug 'b4b4r07/enhancd', \
    use:'enhancd.sh'

zplug 'junegunn/fzf', \
    as:command, \
    do:'./install --bin >/dev/null', \
    use:'bin/fzf', \
    rename_to:'fzf', \
    if:'(( $+commands[go] ))'

zplug "junegunn/fzf-bin", \
    from:gh-r, \
    as:command, \
    use:"*${(L)$(uname -s)}*amd64*", \
    rename_to:fzf, \
    if:'(( ! $+commands[go] ))'

zplug 'NigoroJr/d53982a4d0cf0848985b', \
    from:gist, \
    as:command, \
    use:goocl, \
    do:'chmod 755 goocl'

zplug 'NigoroJr/644ae8775023be82544d', \
    from:gist, \
    as:command, \
    rename_to:sort-du, \
    use:sort_du.rb, \
    do:'chmod 755 sort_du.rb'

zplug 'plugins/golang', \
    from:oh-my-zsh, \
    ignore:oh-my-zsh.sh, \
    nice:10

zplug 'zsh-users/zsh-syntax-highlighting', \
    nice:15

zplug 'rimraf/k', \
    use:k.sh

zplug 'knu/z', \
    use:'z.sh', \
    nice:10

zplug 'zsh-users/zaw'
zplug 'NigoroJr/zaw-z', nice:11, on:'zsh-users/zaw'

zplug 'Tarrasch/zsh-bd'

# enhancd
ENHANCD_COMMAND='e'

# zsh-syntax-highlighting
ZSH_HIGHLIGHT_HIGHLIGHTERS=(brackets)

# z
_Z_NO_RESOLVE_SYMLINKS=1
_Z_NO_COMPLETE_CD=1

# zaw
zstyle ':filter-select' max-lines 10
zstyle ':filter-select' extended-search yes
zstyle ':filter-select' case-insensitive yes
zstyle ':filter-select' rotate-list yes

if ! zplug check; then
    printf "Install plugins? [y/N] "
    if read -q; then
        echo
        zplug install
    else
        echo
    fi
fi

zplug load

# Read aliases
if [ -f ~/.aliases ]; then
    source ~/.aliases
fi

# Compile .zshrc if necessary
if [ ! -f ~/.zshrc.zwc -o ~/.zshrc -nt ~/.zshrc.zwc ]; then
    zcompile ~/.zshrc
fi

# Colors {{{
set_colors() {
    eval $( dircolors )
    export ZLS_COLORS=$LS_COLORS
    zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}
}

case $( uname -s ) in
    Darwin)
        if hash brew 2>/dev/null; then
            gnubin=$(brew --prefix coreutils)/libexec/gnubin
            export PATH=$gnubin:$PATH

            if hash dircolors 2>/dev/null; then
                set_colors
            fi
        else
            # Use GNU ls if available
            if hash gls 2>/dev/null; then
                alias ls='gls'
            else
                alias ls='ls -G'
            fi
        fi
        ;;
    *)
        set_colors
        ;;
esac
# }}}

# In tmux, don't show status bar if there's only one window
_tmux_status_bar() {
    if [[ $TMUX ]]; then
        local switch
        if (( $( tmux list-windows | wc -l ) == 1 )); then
            switch=off
        else
            switch=on
        fi
        tmux set-option status $switch
    fi
}
autoload -U add-zsh-hook
add-zsh-hook precmd _tmux_status_bar

# enable cursor selection
zstyle ':completion:*:default' menu select=1
zstyle ':completion:*:(processes|jobs)' command "ps -u $USER"

# Limit Coredump size
limit coredumpsize 102400

# Options {{{
setopt auto_cd              # cd when only directory is entered
setopt auto_list            # Show list of completion
setopt auto_name_dirs       # Use named dirs automatically
setopt auto_param_keys      # Complete variables
setopt auto_param_slash     # Complete / automatically
setopt auto_pushd
setopt auto_remove_slash    # Remove trailing / automatically
setopt auto_resume          # Resume when suspended command is entered
setopt cdable_vars          # cd to named dirs without ~ at beginning
setopt correct              # Suggest correction
setopt extended_glob
autoload -U is-at-least
is-at-least 5.2 && setopt glob_star_short
setopt hist_ignore_all_dups
setopt hist_ignore_space    # Don't add commands that start with space
setopt hist_reduce_blanks
setopt hist_save_no_dups    # Add only last command on duplicate
setopt hist_verify          # Edit before running history
setopt list_packed          # Compact list
setopt list_types           # Show file types
setopt long_list_jobs       # Set jobs -l as the output for jobs
setopt magic_equal_subst    # Completion like --prefix=/usr etc.
setopt no_beep
setopt no_flow_control      # C-s for incremental forward search
setopt no_nomatch           # Allow things like HEAD^^ in Git repositories
setopt numeric_glob_sort
setopt prompt_subst
setopt pushd_ignore_dups    # Don't pushd the same directory
setopt share_history
unsetopt auto_menu          # Don't change completion with Tab
unsetopt prompt_cr          # Show lines without trailing \n
# }}}

# Key bindings {{{
# emacs keybinding
bindkey -e
bindkey -M emacs "\en" down-line-or-history
bindkey -M emacs "\ep" up-line-or-history

# Better history search
autoload history-search-end
zle -N history-beginning-search-backward-end history-search-end
zle -N history-beginning-search-forward-end history-search-end
bindkey '^P' history-beginning-search-backward-end
bindkey '^N' history-beginning-search-forward-end

# Incremental backward search starting from previously executed command {{{
history-incremental-pattern-search-backward-start-from-previous() {
    CMD=$BUFFER
    zle up-history
    zle history-incremental-pattern-search-backward

    # Was aborted
    # Either works
    #if [ "$KEYS" == "" ] || [ "$KEYS" == "" ] || [ "$KEYS" == "" ]; then
    if (( #KEYS == ##\C-g )) || (( #KEYS == ##\C-c )) || (( #KEYS == ##\C-\\ )); then
        BUFFER=$CMD
    fi
}
zle -N history-incremental-pattern-search-backward-start-from-previous
# }}}

# bindkey '' history-incremental-pattern-search-backward
bindkey '^R' history-incremental-pattern-search-backward-start-from-previous
bindkey '^S' history-incremental-pattern-search-forward

bindkey -M isearch '^H' backward-kill-word
bindkey -M isearch '^R' history-incremental-pattern-search-backward

zmodload zsh/complist
# Start menu comple with Shift-Tab
bindkey "\e[Z" menu-complete
# Use Shift-Tab to reverse traverse completion list
bindkey -M menuselect "\e[Z" reverse-menu-complete

# zaw
# M-z for zaw-history
bindkey "\ez" zaw-history
# C-M-z for zaw-z
bindkey "\e^Z" zaw-z
# C-x ; for zaw
bindkey "^X;" zaw
# }}}

(( $+commands[rbenv] )) && eval "$( rbenv init - )"
(( $+commands[pyenv] )) && eval "$( pyenv init - )"
(( $+commands[plenv] )) && eval "$( plenv init - )"

# Settings for prompt
if [ -f ~/.prompt.zsh ]; then
    source ~/.prompt.zsh
fi

# Read local configs if any
if [ -f ~/.localrc/zshrc ]; then
    source ~/.localrc/zshrc
fi

# vim: foldmethod=marker ft=zsh
